
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

ALTER PROC [dbo].[USP_Full_Load_ASM_UB_RETAIL_REFRESH] AS
BEGIN
--***************************START************************************************
/*******************************************HISTORY**************************************************/
/*--------------------------------------------------------------------------------------------------*/
/*--------------------------------------------------------------------------------------------------*/
/*    DATE   	|	CREATED/MODIFIED BY		|					CHANGE DESCRIPTION					*/
/*--------------------------------------------------------------------------------------------------*/
/*	2024-12-24 	|	Nikita		        | First_Source_Lead_type Changes 		*/
--/*  25/04/2025 |  Lachmanna             |   Same day retail flag added                        */
/*  04/06/2025 |  Ashwini             |   Return logic excluded and new column addition PolicyNo,TotalAmount,PurchaseType,Addons_Status                       */
/*  08/08/2025 |  Ashwini             |   Addition of CityBV, BU, DPP and Grouped_dealercode                        */
/*  20/08/2025 |  Lachmanna             |   FinancerName column added in dim                        */
/*--------------------------------------------------------------------------------------------------*/
/*--------------------------------------------------------------------------------------------------*/
/*******************************************HISTORY**************************************************/
--****************************START*******************************************
--1.Retail Dim:
TRUNCATE TABLE ASM_UB_RETAIL_DIM
TRUNCATE TABLE ASM_UB_RETAIL_STG
TRUNCATE TABLE ASM_UB_RETAIL_FACT

INSERT INTO ASM_UB_RETAIL_DIM
SELECT DISTINCT 
RH.HEADERID AS PK_RETAILHEADERID,
CAST(RH.DOCDATE AS DATE) AS RETAILDATE,
FM.FINANCECATEGORY AS RETAILFINANCIERCATEGORY,
RH.MODEOFPURCHASE AS MODEOFPURCHASE,
ENQUIRY_HEADER.CUSTOMEROWNERSHIPPROFILEID AS ENQUIRYCUSTOMEROWNERSHIP,
FM.FINANCECOMPANY AS FINANCECOMPANY,
RH.ISEXCHANGEAPPLICABLE AS EXCHANGESTATUS,
GETDATE() AS CREATEDDATETIME,
RH.IMPORTEDDATE,
RH.CDMS_BATCHNO,
(CASE
	WHEN ISNULL(RETAIL_HEADER_EXT.IsInstitutionalSale,0) = 1
	AND RH.DocType NOT IN (441,1000088) THEN 'Institutional' 
	WHEN ISNULL(RETAIL_HEADER_EXT.IsInstitutionalSale,0) = 0 AND Len(BRANCH_MASTER.Code) = 10 
	AND SUBSTRING(BRANCH_MASTER.Code,0,6) = '00000' 
	AND ((SUBSTRING(BRANCH_MASTER.Code,6,6) BETWEEN '10000' AND '14000') 
	OR (SUBSTRING(BRANCH_MASTER.Code,6,6) = '25669'))  AND RH.DocType NOT IN (441,1000088) THEN 'Showroom'
	ELSE 'Command Area' 
	END) AS SALESCHANNEL
,RH.FINANCECOMPANY AS FINANCIER	
,RH.INSURANCECOMPANY AS INSURER
,ISNULL(CM.NAME,Fm.FinancerName) as FinancerName
	-- INTO ASM_UB_RETAIL_DIM
FROM 
RETAIL_HEADER RH INNER JOIN COMPANY_MASTER ON (RH.COMPANYID=COMPANY_MASTER.COMPANYID AND COMPANY_MASTER.COMPANYTYPE = 10)
INNER JOIN BRANCH_MASTER ON (RH.BRANCHID=BRANCH_MASTER.BRANCHID)
INNER JOIN RETAIL_LINE ON (RH.HEADERID=RETAIL_LINE.DOCID)
LEFT JOIN BOOKING_LINE ON (RETAIL_LINE.BOOKINGDATALINEID=BOOKING_LINE.LINEID)
LEFT JOIN ENQUIRY_LINE ON (BOOKING_LINE.ENQUIRYDATALINEID=ENQUIRY_LINE.LINEID)
LEFT JOIN ENQUIRY_HEADER ON (ENQUIRY_LINE.DOCID=ENQUIRY_HEADER.HEADERID)
LEFT JOIN RETAIL_LINE_EXT RLE ON (RETAIL_LINE.LINEID=RLE.LINEID)
LEFT JOIN FINANCER_MASTER FM ON (RLE.FINANCERCONTACTID=FM.FINANCERID AND FM.ID = (SELECT MAX(FM1.ID) FROM FINANCER_MASTER FM1 WHERE FM.FINANCERID = FM1.FINANCERID)) 
LEFT OUTER JOIN RETAIL_HEADER_EXT ON (RH.HEADERID=RETAIL_HEADER_EXT.HEADERID)
LEFT JOIN CONTACT_MASTER CM on RLE.FINANCERCONTACTID=CM.ContactID
WHERE
CAST(RH.DOCDATE AS DATE) BETWEEN '2021-01-01' AND Cast(Getdate()-1 as date) AND 
RH.DOCTYPE IN(141,1000079,1000317,1012739,441) 
--AND RH.IMPORTEDDATE>(SELECT MAX(IMPORTEDDATE) FROM ASM_UB_RETAIL_DIM)

DELETE FROM ASM_UB_RETAIL_DIM WHERE RETAILDATE>Cast(Getdate()-1 as date)

--Dedup

  ;WITH CTE AS            
 (                  
  SELECT *,                  
   ROW_NUMBER()OVER(PARTITION BY PK_RETAILHEADERID ORDER BY IMPORTEDDATE DESC)RNK                   
  FROM ASM_UB_RETAIL_DIM                  
 )                  
DELETE FROM CTE             
 WHERE RNK<>1;
--************************************************************************************************
--2.Retail Stage:
--TRUNCATE TABLE ASM_UB_RETAIL_STG

INSERT INTO ASM_UB_RETAIL_STG
SELECT DISTINCT
CM.CODE AS DEALERCODE,
IM.CODE+IV.CODE As SKU,
Cast(0 as int) as FK_DEALERCODE,
Cast(0 as int) as FK_SKU,
10002 As FK_TYPE_ID,
CAST(RH.DOCDATE as Date) AS DATE,
RL.LINEID AS RETAILLINEID,
RL.DOCID AS FK_RETAILDOCID,
CM.COMPANYTYPE AS COMPANYTYPE,
RH.BRANCHID,
Count(Distinct RL.SERIALNO) as ACTUALQUANTITY,
0 AS TARGETQUANTITY,  
getdate() as LASTUPDATEDDATETIME,
RH.IMPORTEDDATE,
RH.CDMS_BATCHNO,
(LEFT(DATENAME( MONTH,RH.DOCDATE),3)+'-'+Cast(Year(RH.DOCDATE) as varchar(4))) As PERIODNAME,
IV.CODE As COLOUR_CODE,
IM.CODE As MODELCODE,
Cast(0 as int) as FK_MODEL,
100021 As FLAG,
Cast(0 as int) as IsSaleReturn,
RH.TEHSIL3W AS TEHSILID,
NULL AS SALESPERSON,
NULL as LeadType,
NULL AS SALESPERSON_ID,
NULL AS First_Source_Lead_type,
NULL AS SourceOfEnquiry,
NULL AS SubSourceOfEnquiry,
NULL AS mx_dse_enquiry_status,
NULL AS mx_Lead_Score,
NULL AS mx_first_lead_classification

--INTO ASM_UB_RETAIL_STG
FROM
   RETAIL_HEADER RH JOIN COMPANY_MASTER CM ON (RH.COMPANYID=CM.COMPANYID AND CM.COMPANYTYPE = 10)
   JOIN RETAIL_LINE RL ON (RH.HEADERID=RL.DOCID)
   JOIN ITEM_MASTER IM ON (IM.ItemId=RL.ItemID)
   LEFT JOIN ITEMVARMATRIX_MASTER IV ON (RL.VARMATRIXID=IV.ITEMVARMATRIXID)
   LEFT JOIN SAP_ZDEALER_TARGET ON (CM.CODE=SAP_ZDEALER_TARGET.DEALER_CODE and MONTH(RH.DOCDATE)=SAP_ZDEALER_TARGET.ZMONTH and YEAR(RH.DOCDATE)=SAP_ZDEALER_TARGET.ZYEAR)
WHERE 
CAST(RH.DOCDATE AS DATE) BETWEEN  '2021-01-01' AND Cast(Getdate()-1 as date)  AND
RH.DOCTYPE IN(141,1000079,1000317,1012739,441) 
--AND RH.IMPORTEDDATE>(SELECT  MAX(IMPORTEDDATE) FROM ASM_UB_RETAIL_STG)
GROUP BY
   CM.CODE,
   IM.CODE+IV.CODE,
   CAST(RH.DOCDATE as Date),  
   RL.LINEID,
   RL.DOCID,
   CM.COMPANYTYPE,
   RH.BRANCHID,
   RH.IMPORTEDDATE,  
   RH.CDMS_BATCHNO,
   (LEFT(DATENAME( MONTH,RH.DOCDATE),3)+'-'+Cast(Year(RH.DOCDATE) as varchar(4))),
   IV.CODE,
   IM.CODE,
   RH.TEHSIL3W

DELETE FROM ASM_UB_RETAIL_STG WHERE DATE>Cast(Getdate()-1 as date)

--Return Logic:
SELECT RL.SALEINVOICELINEID,RL.SERIALNO INTO #ReturnData_UB
FROM 
RETAIL_LINE_RETURN RL INNER JOIN ASM_UB_RETAIL_STG RD ON RD.RETAILLINEID=RL.SALEINVOICELINEID

--Return Data Flag Updation
UPDATE RD
SET RD.IsSaleReturn=1
FROM ASM_UB_RETAIL_STG RD INNER JOIN #ReturnData_UB RL ON RL.SALEINVOICELINEID=RD.RETAILLINEID

--Dedup Process:
   ;WITH CTE AS                  
 (                  
  SELECT *,                  
   ROW_NUMBER()OVER(PARTITION BY FK_RETAILDOCID,RETAILLINEID ORDER BY IMPORTEDDATE DESC)RNK                   
  FROM ASM_UB_RETAIL_STG                   
 )
DELETE FROM CTE                  
 WHERE RNK<>1;
-- Product Master and Dealer Master FK update: ASM_UB_RETAIL_STG
update B set B.FK_SKU=C.PK_SKU from ASM_UB_RETAIL_STG B INNER JOIN ASM_UB_PRODUCT_SKU_DIM C on (B.SKU=C.SKU_CODE) 
update B set B.FK_MODEL=C.PK_Model_Code from ASM_UB_RETAIL_STG B INNER JOIN ASM_UB_PRODUCT_DIM C on (B.MODELCODE=C.MODELCODE)
update B set B.FK_DEALERCODE=C.PK_DEALERCODE from[dbo].ASM_UB_RETAIL_STG B INNER JOIN ASM_UB_DEALER_MASTER_DIM C on (B.DEALERCODE=C.DEALERCODE)
--****************************************************************************
--3.Retail Fact
--TRUNCATE TABLE ASM_UB_RETAIL_FACT

INSERT INTO ASM_UB_RETAIL_FACT
SELECT
Distinct
DEALERCODE
,SKU
,FK_DEALERCODE
,FK_SKU
,FK_TYPE_ID
,DATE
,RETAILLINEID
,FK_RETAILDOCID
,COMPANYTYPE
,BRANCHID
,ACTUALQUANTITY
,TARGETQUANTITY
,LASTUPDATEDDATETIME
,IMPORTEDDATE
,CDMS_BATCHNO
,PERIODNAME
,COLOUR_CODE
,MODELCODE
,FK_MODEL
,FLAG
,TEHSILID
,SALESPERSON
,LeadType
,SALESPERSON_ID
,First_Source_Lead_type
,SourceOfEnquiry
,SubSourceOfEnquiry
,mx_dse_enquiry_status,
mx_Lead_Score,
mx_first_lead_classification,
NUll as First_Mode_Source,
NUll as First_Mode_SubSource,
--,NULL as IS_SAMEDAYRETAIL
NULL as CityBV,
NULL as DPP,
NULL as BU,
NULL as Grouped_dealercode
FROM ASM_UB_RETAIL_STG  
Where IsSaleReturn<>1

/* Addition of Lead Type  Logic*/
SELECT DISTINCT PK_RETAILHEADERID,
coalesce(EH1.LEADTYPE,EH2.LEADTYPE,
LSQ_PBASE1.mx_Enquiry_Mode ,LSQ_PBASE2.mx_Enquiry_Mode) LEADTYPE,
coalesce(LSQ_PBASE1.MX_SALESPERSON_NAME ,LSQ_PBASE2.MX_SALESPERSON_NAME) SALESPERSON,
coalesce(LSQ_PBASE1.OWNERID, LSQ_PBASE2.OWNERID) SALESPERSON_ID,
CASE when Cast(coalesce(PE1.mx_Dealer_Assignment_Date,PE2.mx_Dealer_Assignment_Date) As Date) >='2024-12-01' THEN  
COALESCE (PE1.MX_QUALIFIED_FIRST_SOURCE,PE2.MX_QUALIFIED_FIRST_SOURCE) 
ELSE 'Not Available' END AS First_Source_Lead_type,
coalesce(LSQ_PBASE1.MX_SOURCE_OF_ENQUIRY ,LSQ_PBASE2.MX_SOURCE_OF_ENQUIRY) SourceOfEnquiry,
coalesce(LSQ_PBASE1.mx_Enquiry_Subsource ,LSQ_PBASE2.mx_Enquiry_Subsource) SubSourceOfEnquiry,
coalesce (PE1.mx_dse_enquiry_status, PE2.mx_dse_enquiry_status) mx_dse_enquiry_status,
coalesce (PE1.mx_Lead_Score, PE2.mx_Lead_Score) mx_Lead_Score,
coalesce (PE1.mx_first_lead_classification, PE2.mx_first_lead_classification) mx_first_lead_classification,
ISNULL(COALESCE(PE1.mx_Qualified_Source_of_Enquiry,PE2.mx_Qualified_Source_of_Enquiry), 'Not Available')   AS First_Mode_Source,
ISNULL(COALESCE(PE1.mx_Qualified_Sub_Source,PE2.mx_Qualified_Sub_Source), 'Not Available') AS First_Mode_SubSource
--RETAIL_DIM.RETAILDATE AS  RETAIL_DATE, 
--coalesce(CAST(DATEADD(mi,30,(DATEADD(hh,5,PE1.mx_Dealer_Assignment_Date))) AS DATE),
--CAST(DATEADD(mi,30,(DATEADD(hh,5,PE2.mx_Dealer_Assignment_Date))) AS DATE ), Cast(EH1.DOCDATE As Date), Cast(EH2.DOCDATE As Date)) AS ENQUIRY_DATE
INTO #UB_LEADTYPE_RETAIL
FROM 
ASM_UB_RETAIL_DIM RETAIL_DIM
INNER JOIN  RETAIL_HEADER RH ON (RH.HEADERID=RETAIL_DIM.PK_RETAILHEADERID)
LEFT JOIN RETAIL_LINE RL1 ON (RH.HEADERID=RL1.DOCID)
LEFT JOIN ALLOCATION_LINE AL ON (AL.LINEID=RL1.ALLOCATIONDATALINEID)
LEFT JOIN BOOKING_LINE BL1 ON  (BL1.LINEID=AL.BOOKINGDATALINEID)
LEFT JOIN RETAIL_LINE RL2 ON RL2.DOCID = RH.HEADERID
LEFT JOIN BOOKING_LINE BL2 ON RL2.BOOKINGDATALINEID = BL2.LINEID
LEFT JOIN ENQUIRY_LINE EL1 ON EL1.LINEID = BL1.ENQUIRYDATALINEID
LEFT JOIN ENQUIRY_LINE EL2 ON EL2.LINEID = BL2.ENQUIRYDATALINEID
LEFT JOIN ENQUIRY_HEADER EH1 ON EH1.HEADERID = EL1.DOCID
LEFT JOIN ENQUIRY_HEADER EH2 ON EH2.HEADERID = EL2.DOCID
LEFT JOIN BOOKING_HEADER_EXT BHE1 ON (BL1.HEADERID = BHE1.HEADERID)
LEFT JOIN BOOKING_HEADER_EXT BHE2 ON (BL2.HEADERID = BHE2.HEADERID)
LEFT JOIN LSQ_UB_PROSPECTACTIVITY_EXTENSIONBASE PAE1 
ON (Cast(PAE1.RelatedProspectId+','+PAE1.ProspectActivityExtensionId as varchar(8000))=BHE1.LMSBOOKINGID) and PAE1.ActivityEvent=12000 
LEFT JOIN LSQ_UB_PROSPECT_BASE LSQ_PBASE1 ON (LSQ_PBASE1.ProspectID=PAE1.RelatedProspectId)
LEFT JOIN LSQ_UB_PROSPECTACTIVITY_EXTENSIONBASE PAE2 
ON (Cast(PAE2.RelatedProspectId+','+PAE2.ProspectActivityExtensionId as varchar(8000))=BHE2.LMSBOOKINGID) and PAE2.ActivityEvent=12000 
LEFT JOIN LSQ_UB_PROSPECT_BASE LSQ_PBASE2 ON (LSQ_PBASE2.ProspectID=PAE2.RelatedProspectId) 

LEFT JOIN LSQ_UB_PROSPECT_EXTENSIONBASE PE1 ON (LSQ_PBASE1.ProspectID=PE1.ProspectID) 
LEFT JOIN LSQ_UB_PROSPECT_EXTENSIONBASE PE2 ON (LSQ_PBASE2.ProspectID=PE2.ProspectID) 

WHERE RH.DOCTYPE IN(141,1000079,1000317,1012739,441) 

UPDATE RF 
SET RF.LEADTYPE = LD.LEADTYPE 
,RF.SALESPERSON = LD.SALESPERSON 
,RF.SALESPERSON_ID =LD.SALESPERSON_ID
, RF.First_Source_Lead_type = LD.First_Source_Lead_type,
RF.SourceOfEnquiry =LD.SourceOfEnquiry,
RF.SubSourceOfEnquiry=LD.SubSourceOfEnquiry,
RF.mx_dse_enquiry_status=LD.mx_dse_enquiry_status,
RF.mx_Lead_Score=LD.mx_Lead_Score,
RF.mx_first_lead_classification=LD.mx_first_lead_classification,
RF.First_Mode_Source=LD.First_Mode_Source,
RF.First_Mode_SubSource=LD.First_Mode_SubSource
--,RF.IS_SAMEDAYRETAIL= (CASE WHEN DATEDIFF(Day ,LD.ENQUIRY_DATE,LD.RETAIL_DATE) is not NULL THEN (CASE WHEN DATEDIFF(Day ,LD.ENQUIRY_DATE,LD.RETAIL_DATE)=0 THEN 'YES' ELSE 'NO' END ) END)
FROM 
ASM_UB_RETAIL_FACT RF INNER JOIN #UB_LEADTYPE_RETAIL LD ON RF.FK_RETAILDOCID=LD.PK_RETAILHEADERID


----------------------Addons table Fact----------------------

TRUNCATE TABLE ASM_UB_ADDONS_FACT

INSERT INTO ASM_UB_ADDONS_FACT
SELECT
   --DISTINCT
CM.CODE AS ADDON_DEALERCODE,
CM.NAME AS ADDON_DEALERNAME,
dim.REGION AS ADDON_REGION,
DIM.CITY as ADDON_CITY, 
BM.NAME ADDON_BRANCH, 
HEADER.DOCDATE AS ADDON_RETAILDATE,
CONCAT(YEAR(Header.DOCDATE),'-',MONTH(HEADER.DOCDATE)) FY_YEAR_MONTH,
LINE.CONTRACTNAME, 
LINE.EFFECTIVEDATE, 
LINE.EXPIRYDATE,
HEADER.SALESPERSON,
HEADER.DOCNAME, 
HEADER.INVOICEDOCNAME ,
LINE.MODELCODE, 
LINE.MODELNAME ,
LINE.SerialNo,
VD.CODE AS VDEALERCODE, 
VD.DOCDATE AS VDOCDATE, 
VD.NAME AS VDDEALERNAME, 
VD.BRANCH AS VBRANCH, 
VD.CITY AS VCITY, 
VD.REGION AS VREGION,
HEADER.IMPORTEDDATE,
HEADER.HeaderID,
LINE.LINEID,
BM.BRANCHID,
HEADER.PolicyNo,
HEADER.TotalAmount,
HEADER.PURCHASETYPE,
NULL as ADDONS_STATUS
FROM CDMS_CSD_HEADER HEADER 
JOIN CDMS_CSD_LINE LINE 
ON HEADER.HeaderID=LINE.DocID 
JOIN  INSTALL_BASE_MASTER IB
ON LINE.IBID = IB.IBID
JOIN COMPANY_MASTER CM  
ON CM.COMPANYID = HEADER.CompanyID 
AND CM.COMPANYTYPE=10 
JOIN BRANCH_MASTER BM 
ON BM.BRANCHID=HEADER.BranchID
JOIN ASM_UB_DEALER_MASTER_DIM DIM 
ON DIM.DEALERCODE=CM.CODE 
JOIN
( select RL.SerialNo,RL.COMPANYID, MM.CODE, RH.DOCDATE, MM.NAME, B.NAME AS BRANCH, DM.CITY, DM.REGION  from 
RETAIL_LINE RL 
LEFT JOIN RETAIL_HEADER RH ON RH.HEADERID=RL.DOCID
LEFT JOIN COMPANY_MASTER MM ON RL.COMPANYID=MM.COMPANYID AND MM.COMPANYTYPE=10
JOIN BRANCH_MASTER B 
ON B.BRANCHID=RH.BranchID
LEFT JOIN ASM_UB_DEALER_MASTER_DIM DM 
ON DM.DEALERCODE=MM.CODE
)VD 
on IB.Name=VD.SERIALNO 
WHERE CAST(HEADER.DocDate AS DATE)>='2020-01-01'
AND HEADER.Companyid <> '1051'
AND HEADER.Contactid not in (
select  distinct RHR.Contactid
from CDMS_CSD_HEADER HEADER 
JOIN CDMS_CSD_LINE LINE ON HEADER.HeaderID=LINE.DocID 
JOIN ITEM_MASTER IM ON IM.itemid =LINE.itemid
JOIN RETAIL_LINE_RETURN ON RETAIL_LINE_RETURN.IBID=LINE.IBID 
JOIN RETAIL_HEADER_RETURN RHR ON RHR.Headerid= RETAIL_LINE_RETURN.docid 
JOIN COMPANY_MASTER CM  ON CM.COMPANYID = HEADER.CompanyID AND CM.COMPANYTYPE=10
AND RHR.DocType = 1012747)

--Dedup

  ;WITH CTE AS            
 (                  
  SELECT *,                  
   ROW_NUMBER()OVER(PARTITION BY HEADERID, CONTRACTNAME,LINEID,ADDON_DEALERCODE  ORDER BY IMPORTEDDATE DESC)RNK                   
  FROM ASM_UB_ADDONS_FACT                  
 )             
DELETE FROM CTE             
 WHERE RNK<>1;


---CityBV
UPDATE FT
SET FT.CityBV = CONCAT(DM.CityCode, SUBSTRING(PM.MODELCODE, 3, 4))
FROM ASM_UB_RETAIL_FACT FT
JOIN ASM_UB_DEALER_MASTER_DIM DM ON DM.DEALERCODE = FT.DEALERCODE --and DM.KREGIO
JOIN ASM_UB_PRODUCT_DIM PM ON PM.PK_MODEL_CODE = FT.Fk_MODEL
WHERE FT.FK_TYPE_ID = 10002;
--add null CityBV in INC LOAD

--DPP
update FT set FT.DPP =DP.DPP from
ASM_UB_RETAIL_FACT FT
JOIN ASM_UB_DEALER_MASTER_DIM DM on DM.DEALERCODE = FT.DEALERCODE
JOIN ASM_MC_UB_DPP_MASTER DP on DP.CityBV= FT.CityBV
WHERE FK_TYPE_ID = 10002
and DP.FLAG='URB_SALE';

--BU and grouped dealercode
update FT
set FT.BU ='URB',
    FT.Grouped_dealercode = '00000' + LEFT(FT.Dealercode, LEN(FT.Dealercode) - 1)
FROM ASM_UB_RETAIL_FACT FT
WHERE FT.FK_TYPE_ID = 10002
and  FT.Dealercode LIKE '%C';

END
GO